Script started on Wed 02 Dec 2020 07:58:02 PM PST
[?1034h============================================================== (unix1) 07:58 PM
/afs/cats.ucsc.edu/courses/cse112-wm/Languages/prolog/Examples/
bash 58$ cat -n einstein.pl
     1	% $Id: einstein.pl,v 1.6 2019-03-07 14:21:30-08 - - $ */
     2	
     3	%
     4	% -----------------------------------------------------------------
     5	% 
     6	%                         Einstein's Riddle
     7	% 
     8	% * General Problem:
     9	%   - There are 5 houses in a row
    10	%   - Each house is a different color.
    11	%   - In each house lives a person with a different nationality.
    12	%   - The 5 owners
    13	%        + drink a certain type of beverage,
    14	%        + smoke a certain brand of cigar, and
    15	%        + keep a certain pet.
    16	%   - No owners have the same pet, smoke the same brand of cigar or
    17	%     drink the same beverage.
    18	% 
    19	% * Specific Facts:
    20	%   - The Brit lives in the red house.
    21	%   - The Swede keeps dogs as pets.
    22	%   - The Dane drinks tea.
    23	%   - The green house is on the left of the white house.
    24	%   - The green house's owner drinks coffee.
    25	%   - The person who smokes Pall Mall rears birds.
    26	%   - The owner of the yellow house smokes Dunhill.
    27	%   - The man living in the center house drinks milk.
    28	%   - The Norwegian lives in the first house.
    29	%   - The man who smokes Blends lives next to the one who keeps cats.
    30	%   - The man with the horse lives next to the man who smokes Dunhill.
    31	%   - The owner who smokes Bluemasters drinks beer.
    32	%   - The German smokes Prince.
    33	%   - The Norwegian lives next to the blue house.
    34	%   - The man who smokes Blends has a neighbor who drinks water.
    35	% 
    36	% * Question:
    37	%   - Who owns the fish?
    38	% 
    39	% -----------------------------------------------------------------
    40	%
    41	
    42	%
    43	% Is the first house in a pair next to the second?
    44	%
    45	left_of( Left, Right, [Left, Right | _]).
    46	left_of( Left, Right, [_ | Others]) :- left_of( Left, Right, Others).
    47	
    48	%
    49	% Are the two houses in a pair next to each other?
    50	%
    51	next_to( Left, Right, Houses) :- left_of( Left, Right, Houses).
    52	next_to( Left, Right, Houses) :- left_of( Right, Left, Houses).
    53	
    54	%
    55	% Are each of the facts true about the houses?
    56	%
    57	map_member( [], _).
    58	map_member( [Fact | Facts], Houses) :-
    59	   member( Fact, Houses),
    60	   map_member( Facts, Houses).
    61	
    62	%
    63	% Apply each pair of relations (left_of or next_to) to the houses.
    64	%
    65	map_pairs( _, [], _).
    66	map_pairs( Relation, [First, Second | Rest], Houses) :-
    67	   call( Relation, First, Second, Houses),
    68	   map_pairs( Relation, Rest, Houses).
    69	
    70	%
    71	% Statement of Einstein's riddle in Prolog.
    72	%
    73	einstein( Houses, Fish) :-
    74	   %         HOUSE( NATION   , COLOR , DRINK , SMOKE      , PET  )
    75	   Houses = [house( norwegian, _     , _     , _          , _    ),
    76	             _,
    77	             house( _        , _     , milk  , _          , _    ),
    78	             _,
    79	             _
    80	   ],
    81	   Facts =  [house( brit     , red   , _     , _          , _    ),
    82	             house( swede    , _     , _     , _          , dogs ),
    83	             house( dane     , _     , tea   , _          , _    ),
    84	             house( _        , green , coffee, _          , _    ),
    85	             house( _        , _     , _     , pallmall   , birds),
    86	             house( _        , yellow, _     , dunhill    , _    ),
    87	             house( _        , _     , beer  , bluemasters, _    ),
    88	             house( german   , _     , _     , prince     , _    ),
    89	             house( Fish     , _     , _     , _          , fish )
    90	   ],
    91	   Left =   [house( _        , green , _     , _          , _    ),
    92	             house( _        , white , _     , _          , _    )
    93	   ],
    94	   Next =   [house( _        , _     , _     , blends     , _    ),
    95	             house( _        , _     , _     , _          , cats ),
    96	             house( _        , _     , _     , _          , horse),
    97	             house( _        , _     , _     , dunhill    , _    ),
    98	             house( norwegian, _     , _     , _          , _    ),
    99	             house( _        , blue  , _     , _          , _    ),
   100	             house( _        , _     , _     , blends     , _    ),
   101	             house( _        , _     , water , _          , _    )
   102	   ],
   103	   map_member( Facts, Houses),
   104	   map_pairs( left_of, Left, Houses),
   105	   map_pairs( next_to, Next, Houses).
   106	
   107	%
   108	% Code to print out the answer to the riddle.
   109	%
   110	
   111	riddle :-
   112	   einstein( Houses, Fish),
   113	   write_houses( Houses), nl,
   114	   write_fish( Fish), nl,
   115	   write( '--------------------------------------------------'),
   116	   nl, nl.
   117	
   118	write_fish( Fish) :-
   119	   write( 'The '), write( Fish), write( ' owns the fish.'), nl.
   120	
   121	write_houses( []).
   122	write_houses( [House | Houses]) :-
   123	   write_house( House),
   124	   write_houses( Houses).
   125	
   126	write_house( house( Nation, Color, Drink, Smoke, Pet)) :-
   127	   write_label( 'House', Nation, comma),
   128	   write_label( 'Color', Color, comma),
   129	   write_label( 'Drink', Drink, comma),
   130	   write_label( 'Smoke', Smoke, comma),
   131	   write_label( 'Pet', Pet, period).
   132	
   133	write_label( Label, Object, Punct) :-
   134	   write( Label), write( ': '), write( Object), call( Punct).
   135	
   136	comma :- write( ', ').
   137	period :- write( '.'), nl.
   138	
   139	%
   140	% Print out the answer to the riddle.
   141	%
   142	
   143	% TEST: riddle.
   144	
============================================================== (unix1) 07:58 PM
/afs/cats.ucsc.edu/courses/cse112-wm/Languages/prolog/Examples/
bash 59$ swipl
Welcome to SWI-Prolog (threaded, 64 bits, version 8.0.3)
SWI-Prolog comes with ABSOLUTELY NO WARRANTY. This is free software.
Please run ?- license. for legal details.

For online help and background, visit http://www.swi-prolog.org
For built-in help, use ?- help(Topic). or ?- apropos(Word).

?- [einstein][einstein].
true.

?- riddle.
House: norwegian, Color: yellow, Drink: water, Smoke: dunhill, Pet: cats.
House: dane, Color: blue, Drink: tea, Smoke: blends, Pet: horse.
House: brit, Color: red, Drink: milk, Smoke: pallmall, Pet: birds.
House: german, Color: green, Drink: coffee, Smoke: prince, Pet: fish.
House: swede, Color: white, Drink: beer, Smoke: bluemasters, Pet: dogs.

The german owns the fish.

--------------------------------------------------

true 
ERROR: Type error: `character_code' expected, found `-1' (an integer)
ERROR: In:
ERROR:   [11] char_code(_8628,-1)
ERROR:   [10] '$in_reply'(-1,'?h') at /usr/local/swipl/lib/swipl/boot/init.pl:848
?- ^D
% halt
============================================================== (unix1) 07:58 PM
/afs/cats.ucsc.edu/courses/cse112-wm/Languages/prolog/Examples/
bash 60$ exit

Script done on Wed 02 Dec 2020 07:58:33 PM PST
